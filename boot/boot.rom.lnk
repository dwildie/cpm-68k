OUTPUT_FORMAT(srec)

MEMORY
{
    rom : ORIGIN = 0xFD0000, LENGTH = 0x8000
    ram : ORIGIN = 0xFD8000, LENGTH = 0x8000
}

SECTIONS
{
  
  __stack_size__ = 0x1000;

  __rom_end__ = ORIGIN(rom) + LENGTH(rom) - 1;
  __ram_end__ = ORIGIN(ram) + LENGTH(ram) - 1;
  
  .text : { *(.text*) } > rom
  .rodata : { *(.rodata*) } > rom
  
  __text_end__ = .;
  __free_rom_size__ = __rom_end__ - .;
  
  __data_rom_start__ = .;
  
    .data : {
    __data_start__ = .;
    *(.data*)
  } > ram AT > rom
  
  __data_end__ = .;
  __data_size__ = __data_end__ - __data_start__;
  
  .bss : { 
    __bss_start__ = .;
    *(.bss*) *(COMMON)
    __bss_end__ = .;
    __bss_size__ = __bss_end__ - __bss_start__;
    . = ALIGN(16);
    _end = .; 
  } > ram
  
  . += __stack_size__;
  . = ALIGN (16);
  __stack_init__ = .;
  
  . += 1;
  . = ALIGN (16);
  __free_ram_start__ = .;
  __free_ram_size__ = __ram_end__ - __free_ram_start__;
  
  /DISCARD/ : {
    *(.eh_frame);
    *(.comment);
  }
}
